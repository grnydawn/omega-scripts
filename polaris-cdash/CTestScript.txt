# CTestScript.txt to submit generated XMLs
set(CTEST_PROJECT_NAME omega)
set(CTEST_NIGHTLY_START_TIME "01:00:00 UTC")
set(CTEST_SITE "$ENV{HOSTNAME}")
set(CTEST_BUILD_NAME "ExternalTest_Run")

# Set source and binary directory to current (required for ctest_submit)
set(CTEST_SOURCE_DIRECTORY "${CMAKE_CURRENT_LIST_DIR}")
set(CTEST_BINARY_DIRECTORY "${CMAKE_CURRENT_LIST_DIR}")

# Create Testing/Temporary directory manually for ctest log files
file(MAKE_DIRECTORY "${CTEST_BINARY_DIRECTORY}/Testing/Temporary")

# Initialize CTest to ensure CTEST_BINARY_DIRECTORY is used for logging
ctest_start(Nightly)

# CDash configuration
if(CMAKE_VERSION VERSION_GREATER 3.14)
  set(CTEST_SUBMIT_URL "https://my.cdash.org/submit.php?project=omega")
else()
  set(CTEST_DROP_METHOD "https")
  set(CTEST_DROP_SITE "my.cdash.org")
  set(CTEST_DROP_LOCATION "/submit.php?project=omega")
endif()

set(CTEST_DROP_SITE_CDASH TRUE)

# Define files to submit
set(FILES_TO_SUBMIT)

if(EXISTS "${CMAKE_CURRENT_LIST_DIR}/Build.xml")
    list(APPEND FILES_TO_SUBMIT "${CMAKE_CURRENT_LIST_DIR}/Build.xml")
else()
    message(WARNING "Build.xml not found")
endif()

if(EXISTS "${CMAKE_CURRENT_LIST_DIR}/Test.xml")
    list(APPEND FILES_TO_SUBMIT "${CMAKE_CURRENT_LIST_DIR}/Test.xml")
else()
    message(WARNING "Test.xml not found")
endif()

if(EXISTS "${CMAKE_CURRENT_LIST_DIR}/Done.xml")
    list(APPEND FILES_TO_SUBMIT "${CMAKE_CURRENT_LIST_DIR}/Done.xml")
else()
    message(WARNING "Done.xml not found")
endif()

# Submit files
if(FILES_TO_SUBMIT)
    message(STATUS "Submitting files: ${FILES_TO_SUBMIT}")
    ctest_submit(FILES ${FILES_TO_SUBMIT})
else()
    message(WARNING "No files to submit")
endif()
